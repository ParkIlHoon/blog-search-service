plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.4'
    id 'io.spring.dependency-management' version '1.1.0'

    // test fixture
    id "java-library"
    id "java-test-fixtures"
    id "maven-publish"
}

jar {
    enabled = false
}

bootJar {
    enabled = false
}

allprojects {
    group = 'io.hoon.blog-search'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    // test fixture
    apply plugin: "java-library"
    apply plugin: "java-test-fixtures"
    apply plugin: "maven-publish"

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        implementation 'org.springframework.boot:spring-boot-starter'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testFixturesCompileOnly 'org.projectlombok:lombok'
        testFixturesImplementation 'org.springframework.boot:spring-boot-starter-test'
        testFixturesAnnotationProcessor 'org.projectlombok:lombok'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

}

project(':blog-search-domain-keyword') {
    dependencies {
        implementation project(':blog-search-support-distribute-locker')
    }
}

project(':blog-search-app-api') {
    dependencies {
        implementation project(':blog-search-support-distribute-locker')
        implementation project(':blog-search-support-search-client')
        implementation project(':blog-search-domain-keyword')
    }
}